#############################################################################
 #
 #  This file is part of Verkko, a software program that assembles
 #  whole-genome sequencing reads into telomere-to-telomere
 #  haplotype-resolved chromosomes.
 #
 #  Except as indicated otherwise, this is a 'United States Government
 #  Work', and is released in the public domain.
 #
 #  File 'README.licenses' in the root directory of this distribution
 #  contains full conditions and disclaimers.
 #
 ##

#
#  Rule buildPackages creates input packages for Canu's consensus module.
#
#  Configuration Parameters:
#    ...
#

checkpoint buildPackages:
    input:
        hifi       = HIFI_READS,
        layout     = {rules.layoutContigs.output.layout},
        ont_subset = {rules.extractONT.output.ont_subset} if config['withONT'] == "True" else {rules.emptyfile.output}
    output:
        tigmap     = "7-consensus/packages.tigName_to_ID.map",
        finished   = "7-consensus/packages.finished"
    log:
        err        = '7-consensus/buildPackages.err'
    params:
        jobid      = 1
    threads:
        int(config['par_n_cpus'])
    resources:
        n_cpus = config['par_n_cpus'],
        mem_gb = lambda wildcards, input, attempt: getMemoryRequest(attempt, 'par'),
        time_h = lambda wildcards, input, attempt: getTimeRequest(attempt, 'par')
    shell:
        '''
cd 7-consensus

cat > ./buildPackages.sh <<EOF
#!/bin/sh
set -e

mkdir -p packages

#if [ -e ../{input.ont_subset} ] ; then
#  ont=../{input.ont_subset}
#fi

{VERKKO}/bin/layoutToPackage \\\\
    -layout ../{input.layout} \\\\
    -output packages/part###.cnspack \\\\
    -idmap  packages \\\\
    -partition 0.8 1.5 0.1 \\\\
    -reads ../{input.ont_subset} {input.hifi} \\\\
&& \\\\
touch ../{output.finished}
EOF

#\$ont

chmod +x ./buildPackages.sh

./buildPackages.sh > ../{log.err} 2>&1
        '''

