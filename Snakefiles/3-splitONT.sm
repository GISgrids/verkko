#############################################################################
 #
 #  This file is part of Verkko, a software program that assembles
 #  whole-genome sequencing reads into telomere-to-telomere
 #  haplotype-resolved chromosomes.
 #
 #  Except as indicated otherwise, this is a 'United States Government
 #  Work', and is released in the public domain.
 #
 #  File 'README.licenses' in the root directory of this distribution
 #  contains full conditions and disclaimers.
 #
 ##

#
#  Rule splitONT rewrites the input ONT reads into partitioned FASTA files
#  for mapping to the unitig graph in parallel.
#
#  Configuration Parameters:
#    spl_bases
#    spl_reads
#

checkpoint splitONT:
    input:
        ont    = ONT_READS
    output:
        directory("3-align/split")
    log:
        err    = '3-align/splitONT.err'
    params:
        bases  = config['spl_bases'],
        reads  = config['spl_reads'],
        length = config['spl_min_length'],
        jobid  = 1
    threads:
        int(config['spl_n_cpus'])
    resources:
        n_cpus = config['spl_n_cpus'],
        mem_gb = config['spl_mem_gb'],
        time_h = config['spl_time_h']
    shell:
        '''
cd 3-align

cat > ./splitONT.sh <<EOF
#!/bin/sh
set -e

mkdir -p split

{PYTHON} {VERKKO}/scripts/process_reads.py \\\\
  partition split/ont {params.bases} {params.reads} {params.length} \\\\
  {input.ont}
EOF

chmod +x ./splitONT.sh

./splitONT.sh > ../{log.err} 2>&1
        '''
